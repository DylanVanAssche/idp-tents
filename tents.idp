/*
 * tents.idp
 * Olivier Van den Eede & Dylan Van Assche
 */

vocabulary V {
    // Types
    type Rij isa int
    type Kolom isa int
    type Aantal isa int
    
    // Relaties
    Tenten(Rij, Kolom) 		// Vak is een tent
    Bomen(Rij, Kolom) 		// Vak is een boom
    
    // Getallen naast het speelveld
    AantalPerRij(Rij): Aantal
    AantalPerKolom(Kolom): Aantal
    
 	// Tent verbonden aan boom   
    Verbonden(Rij, Kolom, Rij, Kolom) // Tent rij, Tent kolom, Boom rij, Boom kolom
 
    // Welke vakken liggen naast elkaar
    Buren(Rij, Kolom, Rij, Kolom)
}

structure S: V {
    // 5x5 vierkant, max aantal tenten = 2 per rij of kolom
    Rij = {1 .. 5}
    Kolom = {1 .. 5}
    Aantal = {0 .. 2}
    
    // Rij en kolom starten vanaf 1 in de linkerbovenhoek
    AantalPerRij = {
        1 -> 2;
      	2 -> 0;
        3 -> 2;
        4 -> 0;
        5 -> 1;
    }
    
    AantalPerKolom = {
        1 -> 2;
        2 -> 0;
        3 -> 1;
        4 -> 0;
        5 -> 2;
    }
    
   	Bomen = {
        1,2;
        2,5;
        3,2;
        3,4;
        4,5;
    }
}

theory T: V {
    // Elke tent is verbonden met exact 1 boom (horizontaal of verticaal)
    // Tent mag naast meerdere bomen staan, maar slechts verbonden met 1 boom
    
    
    /** Een tent wordt verbonden met een boom **/
    
    !rb, kb: ?rt, kt: Tenten(rt, kt) & Bomen(rb, kb) & Buren(rt, kt, rb, kb) <=> Verbonden(rt, kt, rb, kb).
    
    !rb kb: Bomen(rb,kb) => (#{rt, kt: Tenten(rt,kt) & Verbonden(rt, kt, rb, kb)} = 1).
    
    //#{rt, kt, rb, kb: Verbonden(rt, kt, rb, kb)} = #{r[Rij], k[Kolom]: Bomen(r,k)}.
    
    
    /** Elke tent mag met slechts 1 boom verbonden worden **/
    //!rt[Rij], kt[Kolom]: Tenten(rt, kt) => #{rb[Rij], kb[Kolom]: Bomen(rb, kb) & Verbonden(rt, kt, rb, kb)} = 1.
    
    
    
    /** evenveel tenten als bomen **/
    // Dit is niet nodig omdat de getallen naast elke rij of kolom dit reeds aangeven
    #{r[Rij], k[Kolom]: Tenten(r,k)} = #{r[Rij], k[Kolom]: Bomen(r,k)}.
    
    
    
    /** De getallen geven aan hoeveel tenten er in de gegeven rij of kolom mogen staan **/
    !r[Rij]: AantalPerRij(r) = #{k[Kolom]: Tenten(r, k)}.
    !k[Kolom]: AantalPerKolom(k) = #{r[Rij]: Tenten(r, k)}.
    
    
    
    /** Tenten mogen elkaar niet raken **/
    // Definieer wat een buur is
    {
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2+1) & (k1 = k2)).	// Niet raken verticaal
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2-1) & (k1 = k2)).
        
		!r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2)   & (k1 = k2+1)).	// Niet raken horizontaal
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2)   & (k1 = k2-1)).
        
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2-1) & (k1 = k2-1)).	// Niet op Hoofddiagonaal
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2+1) & (k1 = k2+1)).
        
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2+1) & (k1 = k2-1)).	// Niet op 2de diagonaal
        !r1, r2, k1, k2: Buren(r1, k1, r2, k2) <- ((r1 = r2-1) & (k1 = k2+1)).
    }
    
    // Tenten mogen geen buren zijn
    !r1, r2, k1, k2: Tenten(r1, k1) & Tenten(r2, k2) => ~Buren(r1, k1, r2, k2).
}

procedure main() {
    stdoptions.nbmodels = 5
    printmodels(modelexpand(T, S))
}